import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'

if(rootProject.file("keystore.properties").exists()) {
    // From https://developer.android.com/studio/publish/app-signing.html
    // to load the signing config file
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "com.jmstudios.redmoon"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 36
        versionName "3.3.2"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            def repo = Grgit.open()
            def commitId = "${repo.log().get(0).getAbbreviatedId()}"
            if (repo.status().isClean()) {
                versionNameSuffix "-$commitId"
            } else {
                versionNameSuffix "-$commitId (dirty)"
            }
        }
    }

    flavorDimensions "dist"
    productFlavors {
        fdroid {
            dimension "dist"
        }
        playstore {
            dimension "dist"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'NewApi'
    }
}

ext {
    supportLib = '28.0.0-rc01'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:design:${supportLib}"
    implementation "com.android.support:support-v4:${supportLib}"
    implementation "com.android.support:appcompat-v7:${supportLib}"
    implementation "com.android.support:support-compat:${supportLib}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'

    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'me.smichel.android:kpreferences:0.11.1'
    playstoreImplementation 'com.github.hotchemi:android-rate:1.0.1'
    //annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
}
